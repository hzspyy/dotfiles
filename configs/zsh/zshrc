#==========#
# Internal #
#==========#

_qc-source() { [[ -r $1 ]] && source $1 }
_qc-eval()   { (( $+commands[$1] )) && smartcache eval $@ }
_qc-comp()   { (( $+commands[$1] )) && smartcache comp $@ }

#=====================#
# Directory Shortcuts #
#=====================#

hash -d config=$XDG_CONFIG_HOME
hash -d cache=$XDG_CACHE_HOME

hash -d zdot=$ZDOTDIR

hash -d Downloads=~/Downloads
#hash -d Workspace=~/Workspace

#for p in ~Workspace/*(/N); hash -d ${p:t}=$p
#hash -d Memo=~/Dropbox/Apps/remotely-save/Main
#hash -d WeChat=~/Documents/WeChat_Data/xwechat_files

#==================#
# Plugins (Part 1) #
#==================#

[[ -d ~zdot/.zcomet ]] || git clone https://github.com/agkozak/zcomet ~zdot/.zcomet/bin

source ~zdot/.zcomet/bin/zcomet.zsh

# Update every 7 days
_qc_last_update=(~zdot/.zcomet/update(Nm-7))
if [[ -z $_qc_last_update ]] {
    touch ~zdot/.zcomet/update
    zcomet self-update
    zcomet update
    zcomet compile ~zdot/*.zsh  # NOTE: https://github.com/romkatv/zsh-bench#cutting-corners
} else {
    # Start p10k instant prompt only when no update
    # Otherwise update logs might not be displayed
    _qc-source ~cache/p10k-instant-prompt-${(%):-%n}.zsh
}

zcomet fpath zsh-users/zsh-completions src
zcomet fpath nix-community/nix-zsh-completions

zcomet load tj/git-extras etc/git-extras-completion.zsh
zcomet load trapd00r/LS_COLORS lscolors.sh
zcomet load QuarticCat/zsh-smartcache
zcomet load chisui/zsh-nix-shell
zcomet load romkatv/zsh-no-ps2

zcomet load ohmyzsh lib clipboard.zsh

AUTOPAIR_SPC_WIDGET=magic-space
AUTOPAIR_BKSPC_WIDGET=backward-delete-char
AUTOPAIR_DELWORD_WIDGET=backward-delete-word
zcomet load hlissner/zsh-autopair

#=========#
# Aliases #
#=========#

_qc-source ~zdot/00_alias.zsh

#============#
# Completion #
#============#
setopt menu_complete  # list choices when ambiguous
#zstyle ":completion:*:*:*:*:*" menu select

zstyle ':completion:*' sort         false                          # preserve inherent orders
zstyle ':completion:*' list-colors  ${(s.:.)LS_COLORS}             # colorize files & folders
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}' 'l:|=* r:|=*'  # auto-cap, substr
zstyle ":completion:*" file-sort change
zstyle ':completion:*' use-cache    true
zstyle ':completion:*' cache-policy _qc-cache-policy
_qc-cache-policy() { local f=("$1"(Nm-7)); [[ -z $f ]] }  # TTL = 7 days

zstyle ':completion:*:descriptions' format '[%d]'
zstyle ':completion:*:messages'     format '%F{yellow}-- %d --%f'
zstyle ':completion:*:warnings'     format '%F{red}-- no matches found --%f'

zstyle ':completion:*:-tilde-:*' tag-order !users  # no `~user`

zstyle ':completion:*:manuals'   separate-sections true  # group candidates by sections
zstyle ':completion:*:manuals.*' insert-sections   true  # `man strstr` -> `man 3 strstr`

zstyle ':completion:*:processes'       command 'ps xwwo pid,user,comm,cmd'  # for kill
zstyle ':completion:*:processes-names' command 'ps xwwo comm'               # for killall
zstyle ':completion:*:process-groups'  hidden  all                          # no `0`


compdef _qc-complete-galias -first-
_qc-complete-galias() {
    [[ $PREFIX != :* ]] && return
    local des=()
    printf -v des '\%s:%s' ${(kv)galiases}
    _describe 'galias' des
}

#compdef _precommand lldb.zsh


if [[ $OSTYPE == darwin* ]] {
    _qc-comp rustup completions zsh rustup
    _qc-comp rustup completions zsh cargo
}


#===========#
# Functions #
#===========#

_qc-source ~zdot/20_functions.zsh



#=======#
# Hooks #
#=======#

autoload -Uz add-zsh-hook

# [PRECMD] Reset cursor shape as some programs (nvim, yazi) will change it
_qc-reset-cursor() {
    print -n '\E[5 q'  # line cursor
}
add-zsh-hook precmd _qc-reset-cursor


#==================#
# Plugins (Part 2) #
#==================#

zcomet compinit
zmodload -i zsh/complist

zcomet load Aloxaf/fzf-tab
#zstyle ':fzf-tab:*' fzf-bindings 'tab:accept'
zstyle ':fzf-tab:*' switch-group '<' '>'
zstyle ':fzf-tab:*' prefix       ''
zstyle ':fzf-tab:complete:kill:argument-rest' fzf-preview 'ps hwwo cmd --pid=$word'
zstyle ':fzf-tab:complete:kill:argument-rest' fzf-flags   '--preview-window=down:3:wrap'
zstyle ':fzf-tab:complete:kill:*'             popup-pad   0 3

zcomet load zsh-users/zsh-autosuggestions

zcomet load zdharma-continuum/fast-syntax-highlighting

unset 'FAST_HIGHLIGHT[chroma-man]'  # buggy: stuck history browsing
unset 'FAST_HIGHLIGHT[chroma-ssh]'  # buggy: incorrect

zcomet load romkatv/powerlevel10k


#==============#
# Key Bindings #
#==============#
autoload -Uz edit-command-line; zle -N edit-command-line
_qc-source ~zdot/30_keymaps.zsh

ZSH_AUTOSUGGEST_MANUAL_REBIND=true
ZSH_AUTOSUGGEST_PARTIAL_ACCEPT_WIDGETS+=(qc-sub-r)
#========#
# Config #
#========#

setopt auto_cd               # simply type dir name to `cd`
setopt interactive_comments  # comments in interactive shells
setopt extended_glob         # extended globbing
setopt glob_dots             # match hidden files, also affect completion
#setopt rc_quotes             # `''` -> `'` within singly quoted strings
setopt magic_equal_subst     # perform filename expansion on `any=expr` args
setopt no_flow_control       # don't occupy [Ctrl+S] and [Ctrl+Q]
setopt SHARE_HISTORY

setopt hist_ignore_all_dups  # no duplicate in history list
setopt hist_save_no_dups     # no duplicate in history file
setopt hist_ignore_space     # ignore commands starting with a space
setopt hist_reduce_blanks    # remove unnecessary spaces
setopt hist_fcntl_lock       # use fcntl to improve locking performance

HISTFILE=~zdot/.zsh_history
HISTSIZE=10000  # number of commands loaded  10000
SAVEHIST=10000  # number of commands stored  10000
KEYTIMEOUT=1  # makes the switch between modes quicker


#=========#
# Scripts #
#=========#

_qc-eval atuin init zsh --disable-up-arrow
_qc-eval direnv hook zsh

_qc-source ~zdot/p10k.zsh

